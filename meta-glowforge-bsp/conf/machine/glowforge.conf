#@TYPE: Machine
#@NAME: Glowforge Basic/Plus/Pro Compatible
#@SOC: i.MX6S
#@DESCRIPTION: Machine configuration for Glowforge Basic/Plus/Pro
#@MAINTAINER: Scott Wiederhold <s.e.wiederhold@gmail.com>
#
# This machine will build for the standard Glowforge CNC laser
# with the imx6 Solo processor.
#
# This image only enables basic functionality, and none of the
# Glowforge proprietary hardware.

MACHINEOVERRIDES =. "mx6:mx6dl:"

require conf/machine/include/imx-base.inc
require conf/machine/include/glowforge.inc
require conf/machine/include/tune-cortexa9.inc

PREFERRED_PROVIDER_virtual/kernel ??= "linux-glowforge"

KERNEL_DEVICETREE = "glowforge.dtb"

UBOOT_MACHINE ?= "glowforge_defconfig"
UBOOT_MAKE_TARGET = "all"
UBOOT_SUFFIX = "imx"
WKS_FILE = "glowforge.wks"

IMAGE_BOOT_FILES += " \
  u-boot.imx uEnv.txt;boot/uEnv.txt \
  zImage;boot/zImage \
  glowforge.dtb;boot/imx6dl-glowforge-v20.dtb \
"

SERIAL_CONSOLE = "115200 ttymxc0"

MACHINE_FIRMWARE_append_mx6 = " linux-firmware-wl18xx"

MACHINE_FEATURES += " wifi"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS += " \
  kernel-module-glowforge \
"
